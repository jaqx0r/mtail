load("@rules_go//go:def.bzl", "go_binary", "go_cross_binary", "go_library")

go_library(
    name = "mtail_lib",
    srcs = ["main.go"],
    importpath = "github.com/jaqx0r/mtail/cmd/mtail",
    visibility = ["//visibility:private"],
    deps = [
        "//internal/exporter",
        "//internal/metrics",
        "//internal/mtail",
        "//internal/waker",
        "@com_github_golang_glog//:glog",
        "@io_opencensus_go//trace",
    ],
)

go_binary(
    name = "mtail",
    embed = [":mtail_lib"],
    visibility = ["//visibility:public"],
    x_defs = {
        "main.Branch": "{STABLE_GIT_BRANCH}",
        "main.Version": "{STABLE_GIT_VERSION}",
        "main.Revision": "{STABLE_GIT_REVISION}",
        "main.EmbedLabel": "{BUILD_EMBED_LABEL}",
    },
)

# Build an mtail binary for supported platforms.
PLATFORMS = [
    "linux_386",
    "linux_amd64",
    "linux_arm64",
    "windows_386",
    "windows_amd64",
    "windows_arm64",
    "darwin_amd64",
    "darwin_arm64",
]

[
    go_cross_binary(
        name = "mtail_" + platform,
        platform = "@rules_go//go/toolchain:" + platform,
        target = ":mtail",
    )
    for platform in PLATFORMS
]

filegroup(
    name = "cross_build",
    srcs = [":mtail_" + platform for platform in PLATFORMS],
)
